#= require ui/application/panes/pane
#= require templates/soundcloud
#= require templates/soundcloud_results
#= require templates/soundcloud_preview

# TODO: proper controller routes for pages and queries
class Warble.SoundcloudSearchView extends Warble.PaneView
  SOUNDCLOUD_CLIENT_ID = "<%= ENV['SOUNDCLOUD_CLIENT_ID'] %>"
  SOUNDCLOUD_SEARCH_URL = "https://api.soundcloud.com/tracks"

  template:              window.JST['templates/soundcloud']
  searchResultsTemplate: window.JST['templates/soundcloud_results']

  events:
    'click #soundlcoud_search'    : 'search'
    'keypress input'              : 'handleEnter'
    'click a.entry'               : 'queueSong'
    'click .scpreview a'          : 'previewAudio'
    'click a#previous_results'    : 'previousPage'
    'click a#next_results'        : 'nextPage'

  initialize: ->
    @searchData = null
    @query      = ''
    @pageSize   = 10
    @startIndex = 0
    
  activate: ->
    @$('#soundcloud_query').focus()

  handleEnter: (event) ->
    if event.which == 13
      this.search event

  previewAudio: (event) ->
    preview_el = @$(event.currentTarget)
    preview_el.parent('div').parent('li').addClass('show-preview')
    preview_el.parent('div').html window.JST['templates/soundcloud_preview']
      url: preview_el.parent('div').data('soundcloud')
    this

  previousPage: (event) ->
    @startIndex -= @pageSize + 1
    this.search event

  nextPage: (event) ->
    @startIndex += @pageSize + 1
    this.search event

  search: (event) ->
    window.workspace.showSpinner()
    q = @$('#soundcloud_query').val()
    
    #reset the search start-index if it's a new search
    if @query != q
      @startIndex = 1
      @query = q
    
    search_params =
      q:            @query
      'limit':      @pageSize
      'offset':     @startIndex if @startIndex >= @pageSize
      'client_id':  SOUNDCLOUD_CLIENT_ID

    $.getJSON SOUNDCLOUD_SEARCH_URL, search_params, (data) =>
      # extract results into a saner object array
      @data = _.map data, (entry, index) ->
        index:          index
        soundcloud_id:  entry.id
        title:          entry.title
        thumbnail:      entry.artwork_url
        author:         entry.user.username
        url:            entry.stream_url + "?client_id=" + SOUNDCLOUD_CLIENT_ID
      
      $('#soundcloud_search_results').html @searchResultsTemplate
        results: @data
        hasPrev: @startIndex > 1
        hasNext: true #Fix this. Need total number of results from soundcloud.
    
      @$el.scrollTop 0
      window.workspace.hideSpinner()
    event.preventDefault()

  queueSong: (event) ->
    console.log @data[$(event.currentTarget).attr('data-id')]
    window.workspace.showSpinner()
    
    $.ajax '/jukebox/playlist',
      type: 'POST'
      data:
        soundcloud: @data[$(event.currentTarget).attr('data-id')]
      success: =>
        window.workspace.hideSpinner()
      error: ->
        window.workspace.navigate '/', true
        window.workspace.hideSpinner()

    event.preventDefault()
